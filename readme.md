
# **Use Terraform to create a Linux VM on Azure**

![architecture](../.asserts/vm.png)
## Connect to labs:

https://app.pluralsight.com/hands-on/playground/cloud-sandboxes

## **Login**
- Login to Azure,Open **Cloud Shell** 
- Select "Bash"
- Check "terraform" is installed
```
terraform version
```
- Generate ssh public and private keys 
```
ssh-keygen
```
![architecture](../.asserts/ssh.png)
## **Deploy VM using terraform**
- Create and change director
```
mkdir terraform
cd terraform
```
- Create files for terraform 
```
touch {providers,main,variables,outputs}.tf
```
 - Create a VM  using files located in terraform folder: https://dev.azure.com/costcocloudops/LTL-PlatformAndOperationsTeam/_git/ltlf-training-and-cohort?path=/terraform
 ![architecture](../.asserts/tf.png)

- Copy contents to main.tf. `vi main.tf` and enter `'i'` to insert contents.Once content are added,click `esc` and `:wq!'.


```
resource "random_pet" "rg_name" {
  prefix = var.resource_group_name_prefix
}

resource "azurerm_resource_group" "rg" {
  location = var.resource_group_location
  name     = random_pet.rg_name.id
}

# Create virtual network
resource "azurerm_virtual_network" "my_terraform_network" {
  name                = "myVnet"
  address_space       = ["10.0.0.0/16"]
  location            = azurerm_resource_group.rg.location
  resource_group_name = azurerm_resource_group.rg.name
}

# Create subnet
resource "azurerm_subnet" "my_terraform_subnet" {
  name                 = "mySubnet"
  resource_group_name  = azurerm_resource_group.rg.name
  virtual_network_name = azurerm_virtual_network.my_terraform_network.name
  address_prefixes     = ["10.0.1.0/24"]
}

# Create public IPs
resource "azurerm_public_ip" "my_terraform_public_ip" {
  name                = "myPublicIP"
  location            = azurerm_resource_group.rg.location
  resource_group_name = azurerm_resource_group.rg.name
  allocation_method   = "Dynamic"
}

# Create Network Security Group and rule
resource "azurerm_network_security_group" "my_terraform_nsg" {
  name                = "myNetworkSecurityGroup"
  location            = azurerm_resource_group.rg.location
  resource_group_name = azurerm_resource_group.rg.name

  security_rule {
    name                       = "SSH"
    priority                   = 1001
    direction                  = "Inbound"
    access                     = "Allow"
    protocol                   = "Tcp"
    source_port_range          = "*"
    destination_port_range     = "22"
    source_address_prefix      = "*"
    destination_address_prefix = "*"
  }
}

# Create network interface
resource "azurerm_network_interface" "my_terraform_nic" {
  name                = "myNIC"
  location            = azurerm_resource_group.rg.location
  resource_group_name = azurerm_resource_group.rg.name

  ip_configuration {
    name                          = "my_nic_configuration"
    subnet_id                     = azurerm_subnet.my_terraform_subnet.id
    private_ip_address_allocation = "Dynamic"
    public_ip_address_id          = azurerm_public_ip.my_terraform_public_ip.id
  }
}

# Connect the security group to the network interface
resource "azurerm_network_interface_security_group_association" "example" {
  network_interface_id      = azurerm_network_interface.my_terraform_nic.id
  network_security_group_id = azurerm_network_security_group.my_terraform_nsg.id
}

# Generate random text for a unique storage account name
resource "random_id" "random_id" {
  keepers = {
    # Generate a new ID only when a new resource group is defined
    resource_group = azurerm_resource_group.rg.name
  }

  byte_length = 8
}

# Create storage account for boot diagnostics
resource "azurerm_storage_account" "my_storage_account" {
  name                     = "diag${random_id.random_id.hex}"
  location                 = azurerm_resource_group.rg.location
  resource_group_name      = azurerm_resource_group.rg.name
  account_tier             = "Standard"
  account_replication_type = "LRS"
}

# Create virtual machine
resource "azurerm_linux_virtual_machine" "my_terraform_vm" {
  name                  = "myVM"
  location              = azurerm_resource_group.rg.location
  resource_group_name   = azurerm_resource_group.rg.name
  network_interface_ids = [azurerm_network_interface.my_terraform_nic.id]
  size                  = "Standard_DS1_v2"

  os_disk {
    name                 = "myOsDisk"
    caching              = "ReadWrite"
    storage_account_type = "Premium_LRS"
  }

  source_image_reference {
    publisher = "Canonical"
    offer     = "0001-com-ubuntu-server-jammy"
    sku       = "22_04-lts-gen2"
    version   = "latest"
  }

  computer_name  = "hostname"
  admin_username = var.username

  admin_ssh_key {
    username   = var.username
    public_key = file("~/.ssh/id_rsa.pub")
  }

  boot_diagnostics {
    storage_account_uri = azurerm_storage_account.my_storage_account.primary_blob_endpoint
  }
}

```


- Copy contents to providers.tf. `vi providers.tf` and enter `'i'` to insert contents.Once content are added,click `esc` and `:wq!'.

```
terraform {
  required_version = ">=1.0"
  required_providers {
    azurerm = {
      source  = "hashicorp/azurerm"
      version = "~>3.0"
    }
    random = {
      source  = "hashicorp/random"
      version = "~>3.0"
    }
  }
}
provider "azurerm" {
  features {}
}

```
- Copy contents to outputs.tf. `vi outputs.tf` and enter `'i'` to insert contents. Once content are added,click `esc` and `:wq!'.


```
output "azureusername" {
  value = azurerm_linux_virtual_machine.my_terraform_vm.admin_username
}

output "public_ip_address" {
  value = azurerm_linux_virtual_machine.my_terraform_vm.public_ip_address
}
```
- Copy contents to variables.tf. `vi variables.tf` and enter `'i'` to insert contents.Once content are added,click `esc` and `:wq!'.
```
variable "resource_group_location" {
  type        = string
  description = "Location for all resources."
  default     = "eastus"
}

variable "resource_group_name_prefix" {
  type        = string
  description = "Prefix of the resource group name that's combined with a random ID so name is unique in your Azure subscription."
  default     = "rg"
}

variable "username" {
  type        = string
  description = "The username for the local account that will be created on the new VM."
  default     = "azureadmin"
}
```



 - Run [terraform init](https://www.terraform.io/docs/commands/init.html) to initialize the Terraform deployment. This command downloads the Azure provider required to manage your Azure resources.

```
terraform init -upgrade
```

- Run [terraform plan](https://www.terraform.io/docs/commands/plan.html) to create an execution plan.
 ```
terraform plan -out main.tfplan

```
- Run terraform apply to apply the execution plan to your cloud infrastructure.

 ```
terraform apply main.tfplan

```

##Connect to virtual machine

- Connect the VM you deployed using terraform using SSH in the Azure Cloud Shell 

```
ssh azureadmin@public_ip
```

## Clean up resources

- Run terraform plan and specify the destroy flag.Run terraform apply to apply plan.

 ```
terraform plan -destroy -out main.destroy.tfplan
terraform apply main.destroy.tfplan

```
